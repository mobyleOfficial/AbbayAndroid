plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def localProperties = new Properties()
def localPropertiesFile = project.rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

android {
    namespace 'com.mobyle.abbay'
    compileSdk 35

    defaultConfig {
        applicationId "com.mobyle.abbay"
        minSdk 28
        targetSdk 35
        versionCode 3
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            storeFile file("C:\\Users\\Abe\\Documents\\Keystore\\keystore.jks")
            storePassword localProperties["KEYSTORE_PASSWORD"]
            keyAlias localProperties["KEY_ALIAS"]
            keyPassword localProperties["KEY_PASSWORD"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose true
        buildConfig true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.1"
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Project modules
    implementation project(':domain')

    // AndroidX Core
    implementation libs.androidx.documentfile
    implementation libs.androidx.core.ktx
    implementation libs.bundles.lifecycle
    implementation libs.androidx.activity.compose
    implementation libs.androidx.startup

    // Compose BOM and UI
    implementation platform(libs.androidx.compose.bom)
    implementation libs.bundles.compose
    implementation libs.androidx.constraintlayout.compose

    // Navigation
    implementation libs.androidx.navigation.compose

    // Media3
    implementation libs.bundles.media3

    // Room
    implementation libs.bundles.room
    kapt libs.androidx.room.compiler

    // Hilt
    implementation libs.bundles.hilt.implementation
    kapt libs.bundles.hilt.kapt

    // Work Manager
    implementation libs.androidx.work.runtime.ktx

    implementation libs.androidx.splashscreen

    // Image Loading
    implementation libs.bundles.coil
    implementation libs.glide.compose

    // Material Design
    implementation libs.material

    // Permissions
    implementation libs.accompanist.permissions

    // Serialization
    implementation libs.kotlinx.serialization.json.legacy
    implementation libs.kotlinx.serialization.json

    // Testing
    testImplementation libs.junit
    androidTestImplementation libs.bundles.testing.android
    androidTestImplementation platform(libs.androidx.compose.bom.test)
    androidTestImplementation libs.bundles.testing.compose
    debugImplementation libs.bundles.testing.compose.debug

    // Firebase
    implementation libs.firebase.crashlytics
    implementation libs.firebase.analytics
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}